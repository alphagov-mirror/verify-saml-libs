apply from: 'publish.gradle'

buildscript {
    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
            jcenter()
        }
        else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    configurations.all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    ext {
        opensaml_version = "3.4.2"
        dropwizard_version = "1.3.9"
        ida_utils_version = '360'
        trust_anchor_version = '1.0-56'
        build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
    }
    group = "uk.gov.ida"
    version = "$build_version"

    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven-test' }
            maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
            jcenter()
        }
        else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }

    configurations {
        slf4j
        jaxb
        guice
        opensaml
        dropwizard
        xml_utils
        test_deps
        security
    }

    dependencies {
        slf4j 'org.slf4j:slf4j-api:1.7.21'

        jaxb 'javax.xml.bind:jaxb-api:2.3.0'

        guice 'com.google.guava:guava:18.0',
                'com.google.inject:guice:4.0'

        dropwizard "io.dropwizard:dropwizard-core:$dropwizard_version",
                "io.dropwizard:dropwizard-client:$dropwizard_version",
                "io.dropwizard:dropwizard-jackson:$dropwizard_version"

        opensaml "org.opensaml:opensaml-core:$opensaml_version",
                "org.opensaml:opensaml-saml-impl:$opensaml_version",
                "org.opensaml:opensaml-xmlsec-api:$opensaml_version",
                "org.opensaml:opensaml-security-api:$opensaml_version",
                "org.opensaml:opensaml-saml-api:$opensaml_version",
                "org.opensaml:opensaml-xmlsec-impl:$opensaml_version",
                'net.shibboleth.utilities:java-support:7.2.0'

        security 'com.nimbusds:nimbus-jose-jwt:5.4',
                "uk.gov.ida:security-utils:2.0.0-$ida_utils_version"

        test_deps "uk.gov.ida:common-test-utils:2.0.0-46",
                "com.github.tomakehurst:wiremock-standalone:2.16.0",
                "io.dropwizard:dropwizard-testing:$dropwizard_version",
                'org.hamcrest:hamcrest-library:1.3',
                'org.assertj:assertj-joda-time:1.1.0',
                'org.assertj:assertj-core:3.9.1',
                'org.junit.jupiter:junit-jupiter-api:5.0.0',
                'uk.gov.ida:ida-dev-pki:1.1.0-37',
                'org.mockito:mockito-core:1.9.5'

        xml_utils "uk.gov.ida:common-utils:2.0.0-$ida_utils_version"
    }
}
